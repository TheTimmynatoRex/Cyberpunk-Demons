ACTOR CPDCyberZombieman
{
		Health 30
		Radius 20
		Height 56
		Speed 10
		PainChance 200
		Monster
		+FLOORCLIP
		SeeSound "CZMSee"
		AttackSound "CZMFire"
		PainSound "CZMPain"
		DeathSound "CZMDeath"
		ActiveSound "CZMActive"
		Obituary "%o was edge-ran over by a Cyber-Zombieman."
		Tag "Cyber-Zombieman"
		DropItem "Clip"
		BloodColor White
		Decal BulletChip
	STATES
	{
		SPAWN:
			CZOM AB 10 A_LOOK
			Loop
		SEE:
			CZOM AABBCCDD 4 A_CHASE
			Loop
		MISSILE:
			CZOM E 10 A_FACETARGET
			CZOM F 8 A_PosAttack
			CZOM E 10 A_Jump(100, "Missile")
			GOTO SEE
		PAIN:
			CZOM G 3
			CZOM G 3 A_PAIN
			GOTO SEE
		DEATH:
			TNT1 A 0 A_NOBLOCKING
			CZOM H 5
			CZOM I 5 A_SCREAM
			CZOM JKLM 5
			CZOM N -1
			STOP
		XDEATH:
			TNT1 A 0 A_NOBLOCKING
			CZOM O 5
			CZOM P 5
			CZOM Q 5
			CZOM RSTUV 5
			CZOM W -1
			STOP
		RAISE:
			CZOM MLKJIH 5
			GOTO SEE
	}
}

ACTOR CPDRapidFireTrooper
{
	//$Category Monsters
   Health 30
   Radius 20
   Height 56
   Speed 8
   PainChance 200
   MONSTER
   +FLOORCLIP
   DropItem Clip
   SeeSound "grunt/sight"
   PainSound "grunt/pain"
   DeathSound "grunt/death"
   ActiveSound "grunt/active"
   Obituary "%o was surprised by the Rapid Fire Trooper's weaponry."
   Tag "Rapid Fire Trooper"
   Decal BulletChip
   States
   {
   Spawn:
      RFTR AB 10 A_Look
      Loop
   See:
      RFTR AABBCCDD 4 A_Chase
      Loop
   Missile:
      RFTR E 10 A_FaceTarget
      RFTR F 5 Bright A_PosAttack
      RFTR E 5 A_CPosRefire
      Goto Missile+1
   Pain:
      RFTR G 3
      RFTR G 3 A_Pain
      Goto See
   Death:
      RFTR H 5
      RFTR I 5 A_Scream
      RFTR J 5 A_Fall
      RFTR K 5
      RFTR L -1
      Stop
    XDeath:
      RFTR M 5
      RFTR N 5 A_XScream
      RFTR N 5
      RFTR P 5 A_Fall
      RFTR QRST 5
      RFTR U -1
      Stop
   Raise:
      RFTR KJ 5
      RFTR IH 5
      Goto See
   }
}

ACTOR CPDPlasmaZombie
{

    Health 50
    Radius 20
    Height 56
    Speed 8
    PainChance 100
    MONSTER 
    +FLOORCLIP
    SeeSound "grunt/sight"
    AttackSound "grunt/attack"
    PainSound "grunt/pain"
    DeathSound "grunt/death"
    ActiveSound "grunt/active"
    Obituary "%o got burned by a Plasma Zombie."
	Tag "Plasma Zombie"
    DropItem Cell
    MissileType PlasmaBall
    States
    {
    Spawn:
        ZMAY AB 10 A_Look
        Loop
    See:
        ZMAY AABBCCDD 4 A_Chase
        Loop
    Missile:
        ZMAY E 10 A_FaceTarget
        ZMAY F 5 A_MissileAttack
        ZMAY E 10 A_Jump(128,"Missile")
        Goto See
    Pain:
        ZMAY G 3
        ZMAY G 3 A_Pain
        Goto See
    Death:
        ZMAY H 5
        ZMAY I 5 A_Scream
        ZMAY J 5 A_Fall
        ZMAY K 5
        ZMAY L -1
        Stop
    XDeath:
        ZMAY O 5
        ZMAY P 5 A_XScream
        ZMAY Q 5 A_Fall
        ZMAY RSTU 5
        ZMAY V -1
        Stop
    Raise:
        ZMAY KJIH 5
        Goto See
    }
}

ACTOR CPDZombieScientistPlasma
{
  obituary "%o was melted by a Zombie Plasma Scientist."
  health 50
  mass 100
  speed 16
  Radius 20
  Height 52
  painchance 150
  seesound "scientist/sight"
  painsound "scientist/pain"
  deathsound "scientist/death"
  activesound "scientist/active"
  MONSTER
  +FLOORCLIP
  +AVOIDMELEE
  +MISSILEMORE
  DropItem Cell
  Tag "Zombie Plasma Scientist"
  States
  {
  Spawn:
    SCZP AB 10 A_Look
    loop
  See:
    SCZP AABBCCDD 3 A_Chase
    loop
  Missile:
    SCZP E 6 A_FaceTarget
    SCZP E 1 A_PlaySound ("PlasmaHi/Fire")
	SCZP F 5 A_CustomMissile ("ZombiePlasmaBall", 45, 3, (1)*Random(-3, 3), CMF_OFFSETPITCH, (0.1)*Random(-3, 3))
	SCZP E 12
    goto See
  Pain:
    SCZP G 3
    SCZP G 3 A_Pain
    goto See
  Death:
    SCZP H 5
    SCZP I 5 A_Scream
    SCZP J 5 A_NoBlocking
    SCZP K 5
    SCZP L 5
    SCZP M 5
    SCZP N -1
    stop
  XDeath:
    SCZP O 5
    SCZP P 5 A_XScream
    SCZP Q 5 A_NoBlocking
    SCZP RSTUV 5
    SCZP W -1
    stop
  Raise:
    SCZP MLKJIH 5
    goto See
  }
}

Actor ZombiePlasmaBall
{
  Height 8
  Radius 6
  Speed 15
  FastSpeed 25
  Projectile
  RenderStyle Add
  Damage (10 + random(1,5))
  Scale 0.4
  DeathSound "weapons/plasmax"
  Decal PlasmaScorch
  States
  {
  Spawn:
    ZPLS AB 2 BRIGHT
	loop
  Death:
    PLSE ABCDE 4 Bright
	Stop
  }
}

ACTOR CPDZombieScientistHeadless
{
  var int user_shots;
  var int user_baseangle;
  var int user_donefirstfire;

  obituary "%o stepped into a Headless Zombie Scientist's line of fire."
  health 40
  gibhealth 16
  mass 90
  speed 15
  Radius 20
  Height 44
  painchance 220
  seesound "monster/SZHDWAKE"
  painsound "monster/SZHDPAIN"
  deathsound "monster/SZHDDEAD"
  activesound "monster/SZHDSNRL"
  MeleeRange 64
  MONSTER
  +FLOORCLIP
  +AVOIDMELEE
  +MISSILEMORE
  +MISSILEEVENMORE
  +DROPOFF
  DropItem "Clip"
  Tag "Headless Zombie Scientist"
  States
  {
  Spawn:
    SZHD AB 10 A_Look
    loop
  See:
	SZHD A 0 A_JumpIf(user_donefirstfire != 1, "PrepareFirstFire")
	SZHD ABCD 5 A_Chase
    loop
  PrepareFirstFire:
    //We've woken! If we can see the player from where we are, fire at random
	SZHD A 0 A_Chase //This ensures the "see" sound plays first time
	SZHD E 0 A_SetUserVar("user_donefirstfire", 1)
	SZHD E 0 A_FaceTarget
	SZHD E 0 A_JumpIfTargetInLOS("Missile")
	goto See
  Melee:
    //On melee, fire all you have at where you think the player is!
	SZHD E 0 A_SetUserVar("user_donefirstfire", 1)
	SZHD E 0 A_SetUserVar("user_shots", random(7, 10))
	SZHD E 0 A_PlaySound("10MM/Cock")
    SZHD E 20 A_FaceTarget
	MeleeLoop:
		SZHD E 1 A_PlaySound ("10MM/FIRE")
		SZHD F 2 BRIGHT A_CustomBulletAttack(0, 0, 1, 3*random(1,3), "BulletPuff", 0, CBAF_AIMFACING | CBAF_NORANDOM)
		SZHD E 0 A_SetUserVar("user_shots", user_shots-1)
		SZHD E 0 A_JumpIf(user_shots > 0,"MeleeLoop")
	SZHD E 35
	goto See
  Missile:
	SZHD E 0 A_SetUserVar("user_shots", random(3, 5))
    SZHD E 0 A_FaceTarget
	SZHD E 0 A_SetUserVar("user_baseangle", angle)
	SZHD E 6 A_SetAngle (user_baseangle + random(-25, 25))
    MissileLoop:
		SZHD E 1 A_PlaySound ("10MM/FIRE")
		SZHD F 3 BRIGHT A_CustomBulletAttack(0, 0, 1, 3*random(1,3), "BulletPuff", 0, CBAF_AIMFACING | CBAF_NORANDOM)
		SZHD E 0 A_SetUserVar("user_shots", user_shots-1)
		SZHD E 0 A_SetAngle (user_baseangle + random(-25, 25))
		SZHD E 0 A_JumpIf(user_shots > 0,"MissileLoop")
	SZHD E 6
    goto See
  Pain:
    SZHD G 3
    SZHD G 3 A_Pain
    goto See
  Death:
    SZHD H 5
    SZHD I 5 A_Scream
    SZHD J 5 A_NoBlocking
    SZHD K 5
    SZHD L 5
    SZHD M 5
    SZHD N -1
    stop
  XDeath:
    SZHD O 5
    SZHD P 5 A_XScream
    SZHD Q 5 A_NoBlocking
    SZHD RSTUV 5
    SZHD W -1
    stop
  Raise:
    SZHD MLKJIH 5
    goto See
  }
}

Actor CPDSonicRailgunZombie 
{ 
  Health 60
  Radius 20
  Height 56
  Speed 8
  PainChance 200
  Monster
  +FloorClip
  Obituary "%o was sniped by a Sonic Railgun Zombie."
  SeeSound "Grunt/Sight"
  PainSound "Grunt/Pain"
  DeathSound "Grunt/Death"
  ActiveSound "Grunt/Active"
  Decal RailScorchLower
  Dropitem "Cell" 224
  Tag "Sonic Railgun Zombie"
  States
  {
  Spawn:
    SRZM AAAAAAAAAABBBBBBBBBB 1 A_Look
    Loop
  See:
    SRZM A 0 A_JumpIfInventory("SRCharge", 10, 3)
    SRZM A 0 A_JumpIfInventory("Charging", 1, "See2")
    SRZM A 0 A_Jump(32, "Charge")
    SRZM AABBCCDD 4 A_Chase
    Loop
  See2:
    SRZM A 0 A_JumpIfInventory("SRCharge", 10, "See")
    SRZM A 0 A_GiveInventory("SRCharge", 1)
    SRZM AA 4 A_Chase("", "")
    SRZM A 0 A_JumpIfInventory("SRCharge", 10, "See")
    SRZM B 0 A_GiveInventory("SRCharge", 1)
    SRZM BB 4 A_Chase("", "")
    SRZM A 0 A_JumpIfInventory("SRCharge", 10, "See")
    SRZM C 0 A_GiveInventory("SRCharge", 1)
    SRZM CC 4 A_Chase("", "")
    SRZM A 0 A_JumpIfInventory("SRCharge", 10, "See")
    SRZM D 0 A_GiveInventory("SRCharge", 1)
    SRZM DD 4 A_Chase("", "")
    Loop
  Charge:
    SRZM A 0 A_GiveInventory("Charging", 1)
    SRZM A 0 A_PlaySound("SRailgunZombie/Charge")
    Goto See2
  Missile:
    SRZM E 0 A_JumpIfInventory("SRCharge", 10, "Missile2")
    SRZM EEE 5 A_FaceTarget 
    SRZM E 3 A_FaceTarget
    SRZM F 0 A_PlaySound("SRailgunZombie/Fire")
    SRZM F 2 A_CustomRailgun(Random(3, 8), 0, "None", "ff 00 00", 1, 1, 1)
    SRZM E 3 A_FaceTarget
    SRZM F 0 A_PlaySound("SRailgunZombie/Fire")
    SRZM F 2 A_CustomRailgun(Random(3, 8), 0, "None", "ff 66 00", 1, 1, 1)
    SRZM F 0 A_CPosReFire
    Goto Missile+4
  Missile2:
    SRZM EEE 5 A_FaceTarget 
    SRZM F 2 A_FaceTarget
    SRZM F 0 A_TakeInventory("Charging", 1)
    SRZM F 0 A_TakeInventory("SRCharge", 10)
    SRZM F 0 A_PlaySound("SRailgunZombie/ChargeFire")
    SRZM F 3 A_CustomRailgun(Random(30, 50), 0, "Red", "Orange", 1, 1)
    SRZM EEE 5 A_FaceTarget
    Goto See
  Pain: 
    SRZM G 3
    SRZM G 3 A_Pain 
    Goto See 
  Death: 
    SRZM H 5 
    SRZM I 5 A_Scream 
    SRZM J 5 A_Fall 
    SRZM K 5 
    SRZM LM 16
    SRZM N -1
    Stop
  XDeath: 
    SRZM O 5      
    SRZM P 5 A_XScream       
    SRZM Q 5 A_Fall        
    SRZM RSTUV 5 
    SRZM W -1 
    Stop 
  Raise: 
    SRZM LKJIH 5 
    Goto See 
  } 
}

Actor Charging : Inventory { Inventory.MaxAmount 1 }

Actor SRCharge : Inventory { Inventory.MaxAmount 10 }

actor CPDShadowTrooper
{
  Game Doom
  //$Category Monsters
  //$Title "Shadow Trooper"
  RenderStyle Translucent
  Alpha 0.4
  Health 60
  Radius 20
  Height 56
  Mass 100
  Speed 10
  PainChance 180
  Monster
  +FLOORCLIP
  +SHADOW
  +SEEINVISIBLE //note: disable this if your current version of zDoom/gzDoom doesn't support it, or update your version!
  Decal "BulletChip"
  SeeSound "ShadowTrooperSight"
  AttackSound "ShadowTrooperAttack"
  PainSound "ShadowTrooperPain"
  DeathSound "ShadowTrooperDeath"
  ActiveSound "ShadowTrooperActive"
  Obituary "%o was assassinated by a Shadow Trooper."
  DropItem "Clip", 255
  DropItem "Clip", 128
  DropItem "Infrared", 40
  Tag "Shadow Trooper"
  States
  {
  Spawn:
    SHDT AB 10 A_Look
    Loop
  See:
    SHDT A 3 A_Chase
	TNT1 A 0 A_FadeTo(0.4, 0.2, false)
	SHDT A 3 A_Chase
	TNT1 A 0 A_FadeTo(0.4, 0.2, false)
	SHDT B 3 A_Chase
	TNT1 A 0 A_FadeTo(0.4, 0.2, false)
	SHDT B 3 A_Chase
	TNT1 A 0 A_FadeTo(0.4, 0.2, false)
	SHDT C 3 A_Chase
	TNT1 A 0 A_FadeTo(0.4, 0.2, false)
	SHDT C 3 A_Chase
	TNT1 A 0 A_FadeTo(0.4, 0.2, false)
	SHDT D 3 A_Chase
	TNT1 A 0 A_FadeTo(0.4, 0.2, false)
	SHDT D 3 A_Chase
	TNT1 A 0 A_FadeTo(0.4, 0.2, false)
	TNT1 A 0 A_Jump(64, "Flicker")
    Loop
  Flicker:
    SHDT A 3 A_Chase
	TNT1 A 0 A_FadeTo(1.0, 0.2, false)
	SHDT A 3 A_Chase
	TNT1 A 0 A_FadeTo(1.0, 0.2, false)
	SHDT B 3 A_Chase
	TNT1 A 0 A_FadeTo(1.0, 0.2, false)
	SHDT B 3 A_Chase
	TNT1 A 0 A_FadeTo(1.0, 0.2, false)
	SHDT C 3 A_Chase
	TNT1 A 0 A_FadeTo(1.0, 0.2, false)
	SHDT C 3 A_Chase
	TNT1 A 0 A_FadeTo(1.0, 0.2, false)
	SHDT D 3 A_Chase
	TNT1 A 0 A_FadeTo(1.0, 0.2, false)
	SHDT D 3 A_Chase
	TNT1 A 0 A_FadeTo(1.0, 0.2, false)
	TNT1 A 0 A_Jump(64, "Flicker")
	Goto See
  Missile:
    SHDT E 2 A_FaceTarget
	TNT1 A 0 A_FadeTo(1.0, 0.2, false)
	SHDT E 2 A_FaceTarget
	TNT1 A 0 A_FadeTo(1.0, 0.2, false)
	SHDT E 2 A_FaceTarget
	TNT1 A 0 A_FadeTo(1.0, 0.2, false)
    SHDT E 4 A_FaceTarget
	SHDT F 4 bright A_CustomBulletAttack(13, 2, 1, random(1,5) * 3, "BulletPuff", 0, CBAF_NORANDOM)
	SHDT E 3 A_FaceTarget
	SHDT F 4 bright A_CustomBulletAttack(13, 2, 1, random(1,5) * 3, "BulletPuff", 0, CBAF_NORANDOM)
	SHDT E 3 A_FaceTarget
	SHDT F 4 bright A_CustomBulletAttack(13, 2, 1, random(1,5) * 3, "BulletPuff", 0, CBAF_NORANDOM)
	SHDT EEE 2 A_FadeTo(0.4, 0.2, false)
	SHDT E 2
	Goto See
  Pain:
	TNT1 A 0 A_FadeTo(1.0, 1.0, false)
	SHDT G 3
	SHDT G 3 A_Pain
    Goto See
  Death:
    SHDT H 5
	TNT1 A 0 A_FadeTo(1.0, 0.2, false)
    SHDT I 5 A_Scream
	TNT1 A 0 A_FadeTo(1.0, 0.2, false)
    SHDT J 5 A_NoBlocking
	TNT1 A 0 A_FadeTo(1.0, 0.2, false)
    SHDT K 5
    SHDT L -1
    Stop
  XDeath:
    SHDT M 5
	TNT1 A 0 A_FadeTo(1.0, 0.2, false)
    SHDT N 5 A_XScream
	TNT1 A 0 A_FadeTo(1.0, 0.2, false)
    SHDT O 5 A_NoBlocking
	TNT1 A 0 A_FadeTo(1.0, 0.2, false)
    SHDT PQRST 5
    SHDT U -1
    Stop
  Raise:
    SHDT K 5
    SHDT JIH 5 A_FadeTo(0.4, 0.2, false)
    Goto See
  }
}

ACTOR CPDRocketGuy 3202
{ 
   Health 50 
   Radius 20 
   Height 56 
   Mass 100 
   Speed 8 
   PainChance 170 
   Monster 
   +FLOORCLIP 
   SeeSound "grunt/sight" 
   PainSound "grunt/pain" 
   Deathsound "grunt/death" 
   ActiveSound "grunt/active"
   DropItem RocketAmmo
   DropItem "RocketLauncher", 32
   Obituary "%o was splattered by a Rocket Zombie." 
   Tag "Rocket Zombie"
   States 
   { 
   Spawn: 
      MPOS AB 10 A_Look 
      Loop 
   See: 
      MPOS AABBCCDD 3 A_Chase 
      Loop 
   Missile: 
      MPOS E 10 A_FaceTarget 
      MPOS F 5 A_CyberAttack 
      MPOS G 10 
      Goto See 
   Pain: 
      MPOS H 2 
      MPOS H 2 A_Pain 
      Goto See 
   Death: 
      MPOS I 5 
      MPOS J 5 A_Scream 
      MPOS K 5 
      MPOS L 5 A_Fall 
      MPOS M -1 
      Stop 
   XDeath: 
      MPOS N 5 
      MPOS O 5 A_XScream 
      MPOS P 5 
      MPOS Q 5 A_Fall 
      MPOS RSTU 5 
      MPOS V -1    
      Stop 
   Raise: 
      MPOS MLKJI 5 
      Goto See 
   } 
}

