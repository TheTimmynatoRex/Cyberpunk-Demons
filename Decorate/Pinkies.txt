ACTOR CPDMechDemon
{ 
	//$Category Monsters
    Health 300 
    Speed 12 
    Height 56
    Radius 30
    Mass 400
    PainChance 180
    Meleedamage 5 
    REACTIONTIME 8 
    MONSTER
    +FLOORCLIP
    SeeSound "blooddemon/sight"
    PainSound "blooddemon/pain"
    DeathSound "blooddemon/death"
    ActiveSound "blooddemon/active"
    //MeleeSound "blooddemon/melee"
    HitObituary "%o was chewed up and spat out by a Mech-Demon."
	Tag "Mech-Demon"
    States 
    { 
    Spawn: 
       SRG2 AB 10 A_Look
       Loop 
    See: 
       SRG2 A 0 A_PlaySound("blooddemon/walk")
       SRG2 AABB 2 A_Chase
       SRG2 C 0 A_PlaySound("blooddemon/walk")
       SRG2 CCDD 2 A_Chase
       Loop 
    Melee: 
	   SRG2 E 0 A_PlaySound("blooddemon/melee")
       SRG2 EF 8 A_FaceTarget
       SRG2 G 8 A_MeleeAttack
       Goto See 
    Pain: 
       SRG2 H 2
       SRG2 H 2 A_Pain
       Goto See
    Death: 
       SRG2 I 8 
       SRG2 I 0 A_FaceTarget
       SRG2 J 0 A_SpawnItemEx("BloodDemonArm", 10, 0, 32, 0, 8, 0, 0, 128)
       SRG2 J 8 A_Scream
       SRG2 K 4 
       SRG2 L 4 A_NoBlocking
       SRG2 M 4 
       SRG2 N -1 
       Stop 
    Raise: 
       SRG2 NMLKJI 5 
       Goto See 
    } 
}

ACTOR BloodDemonArm
{
	Radius 8
	Height 8
	Speed 8
	+DOOMBOUNCE
	+DROPOFF
	+Missile
      States
	{
	Spawn:
         SG2A ABCDEFGH 2
         Loop
	Death:
	 SG2A I -1
	 Loop
	}
}

//This guy's decorate is _Very_ close to the Blood Demon's...
//IE i used it and changed some stuff and added some states.
ACTOR CPDCyberFiend
{ 
    Health 500 
    Speed 10 
    Height 56
    Radius 30
    Mass 400
    PainChance 125
    Meleedamage 8 
    MONSTER
    +FLOORCLIP +MISSILEMORE +MISSILEEVENMORE
    SeeSound "CyberFiend/sight"
    PainSound "CyberFiend/pain"
    DeathSound "CyberFiend/death"
    ActiveSound "CyberFiend/active"
    HitObituary "%o Became the Cyber Fiend's dinner." 
    Obituary "The Cyber Fiend puked on %o."
	Tag "Cyber Fiend"
    States 
    { 
    Spawn: 
       CYFN AB 10 A_Look
       Loop 
    See: 
       CYFN A 0 A_PlaySound("CyberFiend/walk")
       CYFN AABB 2 A_Chase
       CYFN C 0 A_PlaySound("CyberFiend/walk")
       CYFN CCDD 2 A_Chase
       Loop 
    Missile:
       TNT1 A 0 A_JumpifCloser(256,1)
       goto see
       TNT1 A 0 A_Jump(256, "Puke1", "Puke2", "Puke3")
    Puke1:
       CYFN E 0 A_PlaySound("CyberFiend/Attack")
       CYFN EF 8 A_FaceTarget
       CYFN G 8 A_CustomMissile("Puke1")
       Goto See      
    Puke2:
       CYFN E 0 A_PlaySound("CyberFiend/Attack")
       CYFN EF 8 A_FaceTarget
       CYFN G 8 A_CustomMissile("Puke2")
       Goto See      
    Puke3:
       CYFN E 0 A_PlaySound("CyberFiend/Attack")
       CYFN EF 8 A_FaceTarget
       CYFN G 8 A_CustomMissile("Puke3")
       Goto See      
    Melee: 
       CYFN E 0 A_PlaySound("CyberFiend/Attack")
       CYFN EF 8 A_FaceTarget
       CYFN G 8 A_MeleeAttack
       Goto See 
    Pain: 
       CYFN H 2
       CYFN H 2 A_Pain
       Goto See
    Death: 
       CYFN I 8 A_Scream
       CYFN I 0 A_FaceTarget
       CYFN J 0 A_SpawnItemEx("CyberFiendArm", 10, 0, 32, 0, 8, 0, 0, 128)
       CYFN J 8 
       CYFN K 4 
       CYFN L 4 A_NoBlocking
       CYFN M 4 
       CYFN N -1 
       Stop 
    Raise: 
       CYFN NMLKJI 5 
       Goto See 
    } 
}


//origional Blood demon Arm Decorate, it works so why not use it?
ACTOR CyberFiendArm
{
	Radius 8
	Height 8
	Speed 8
	+DOOMBOUNCE
	+DROPOFF
	+Missile
      States
	{
	Spawn:
         CYF1 ABCDEFGH 2
         Loop
	Death:
	 CYF1 I -1
	 Loop
	}
}
//FYI it actually pukes out body parts, a pinky's hand, a cacodemon eye and a player skull.
ACTOR Puke1
{
Radius 8
Height 8
Speed 16
Damage 8
bouncefactor 0.8 
bouncecount 8 
+DOOMBOUNCE
+DROPOFF
+Missile
States
{
Spawn:
PUK1 ABCDEFGH 2
Loop
Death:
TNT1 A 0 A_PlaySound("misc/gibbed")
TNT1 A 0 A_SpawnDebris("1Gib")
TNT1 A 0 A_SpawnDebris("2Gib")
TNT1 A 0 A_SpawnDebris("3Gib")
TNT1 A 0 A_SpawnDebris("1Gib")
TNT1 A 0 A_SpawnDebris("2Gib")
TNT1 A 0 A_SpawnDebris("3Gib")
TNT1 A 1 A_FadeOut(1)
Stop
}
}
ACTOR Puke2
{
Radius 8
Height 8
Speed 16
Damage 8
bouncefactor 0.8 
bouncecount 8 
+DOOMBOUNCE
+DROPOFF
+Missile
States
{
Spawn:
PUK2 ABCDEFGH 2
Loop
Death:
TNT1 A 0 A_PlaySound("misc/gibbed")
TNT1 A 0 A_SpawnDebris("1Gib")
TNT1 A 0 A_SpawnDebris("2Gib")
TNT1 A 0 A_SpawnDebris("3Gib")
TNT1 A 0 A_SpawnDebris("1Gib")
TNT1 A 0 A_SpawnDebris("2Gib")
TNT1 A 0 A_SpawnDebris("3Gib")
TNT1 A 1 A_FadeOut(1)
Stop
}
}
ACTOR Puke3
{
Radius 8
Height 8
Speed 16
Damage 8
bouncefactor 0.8 
bouncecount 8 
+DOOMBOUNCE
+DROPOFF
+Missile
States
{
Spawn:
PUK3 A 2
Loop
Death:
TNT1 A 0 A_PlaySound("misc/gibbed")
TNT1 A 0 A_SpawnDebris("IGib")
TNT1 A 0 A_SpawnDebris("IGib2")
TNT1 A 0 A_SpawnDebris("IGib")
TNT1 A 0 A_SpawnDebris("IGib2")
TNT1 A 0 A_SpawnDebris("IGib")
TNT1 A 0 A_SpawnDebris("IGib2")
TNT1 A 1 A_FadeOut(1)
Stop
}
}

Actor 1GIB
{
health 1
radius 6
height 6
mass 1
+CORPSE
+NOTELEPORT
States
{
Spawn:
1GIB A 4
loop
Crash:
1GIB A 1050
TNT1 A 1 A_FadeOut(1)
Stop
}
}
Actor 2GIB
{
health 1
radius 6
height 6
mass 1
+CORPSE
+NOTELEPORT
States
{
Spawn:
2GIB A 4
loop
Crash:
2GIB A 1050
TNT1 A 1 A_FadeOut(1)
Stop
}
}
Actor 3GIB
{
health 1
radius 6
height 6
mass 1
+CORPSE
+NOTELEPORT
States
{
Spawn:
3GIB A 4
loop
Crash:
3GIB A 1050
TNT1 A 1 A_FadeOut(1)
Stop
}
}
Actor IGIB
{
health 1
radius 6
height 6
mass 1
+CORPSE
+NOTELEPORT
States
{
Spawn:
IGIB A 4
loop
Crash:
IGIB A 1050
TNT1 A 1 A_FadeOut(1)
Stop
}
}
Actor IGIB2
{
health 1
radius 6
height 6
mass 1
+CORPSE
+NOTELEPORT
States
{
Spawn:
IGIB B 4
loop
Crash:
IGIB B 1050
TNT1 A 1 A_FadeOut(1)
Stop
}
}


//pointless text is pointless...

Actor CPDBloodfiend : Demon
{
  Health 250
  PainChance 140
  Speed 12
  Radius 30
  Height 56
  Mass 450
  Monster
  +FloorClip
  +LongMeleeRange
  +DontHurtSpecies
  Obituary "%o died from the toxic blood of a Blood Fiend."
  HitObituary "%o was eaten by a Blood Fiend."
  SeeSound "Monster/sg2sit"
  AttackSound "Monster/sg2atk"
  PainSound "demon/Pain"
  DeathSound "demon/Death"
  ActiveSound "demon/sg2act"
  Tag "Blood Fiend"
  States
  {
  Spawn:
    SAR2 AB 10 A_Look
    Loop
  See:
    SAR2 AABBCCDD 2 A_Chase
    Loop
  Melee:
    SAR2 EF 8 A_FaceTarget
    SAR2 G 8 A_SargAttack
    Goto See
  Missile:
    SAR2 E 0 A_Jumpifcloser(224, 6)
    SAR2 GF 8 A_FaceTarget
    SAR2 E 0 A_PlaySound("Weapons/bloodf")
    SAR2 E 0 A_CustomMissile("Bloodshot",22,0,-1,0,0)
    SAR2 E 8 A_CustomMissile("Bloodshot",22,0,1,0,0)
    Goto See
    SAR2 E 4 A_FaceTarget 
    SAR2 F 10 A_SkullAttack 
    SAR2 G 5 A_Gravity
    Goto See 
  Pain:
    SAR2 H 2
    SAR2 H 2 A_Pain
    Goto See
  Death:
    SAR2 I 8
    SAR2 J 8 A_Scream
    SAR2 K 4
    SAR2 L 4 A_NoBlocking
    SAR2 M 4
    SAR2 N -1
    Stop
  XDeath:
    SAR2 O 5
    SAR2 P 5 A_XScream
    SAR2 Q 5 A_NoBlocking
    SAR2 RSTUV 5
    SAR2 W -1
    Stop
  Raise:
    SAR2 NMLKJI 5
    Goto See
  }
}

Actor Bloodshot
{
  Radius 3
  Height 3
  Speed 20
  Damage 2
  Projectile
  RenderStyle Translucent
  Alpha 0.80
  +ThruGhost
  DeathSound "weapons/bloodx"
  DamageType "Poison"
  States
  {
  Spawn:
    FBLD A 2 Bright A_CStaffMissileSlither
    FBLD A 0 A_SpawnItemEx("Bloodtrail",0,0,0,0,0,0,128)
    FBLD B 2 Bright A_CStaffMissileSlither
    FBLD B 0 A_SpawnItemEx("Bloodtrail",0,0,0,0,0,0,128)
    Loop
  Death:
    FBLD CDE 3 Bright
    Stop
  }
}

Actor Bloodtrail
{
  Radius 1
  Height 1
  Projectile
  RenderStyle Translucent
  Alpha 0.67
  States
  {
  Spawn:
    TNT1 A 3
    FBLD FGH 3 BRIGHT
    Stop
  }
}

ACTOR CPDChaingunDemon
{ 
	//$Category Monsters
	//$Sprite CRG2A1
	//$Title "Chaingun Demon"
    Health 200
    Speed 10
    Height 56
    Radius 30
    Mass 400
    PainChance 160
	MinMissileChance 64
    REACTIONTIME 8 
    MONSTER
    +FLOORCLIP
	+NEVERRESPAWN
	-COUNTKILL
    SeeSound "blooddemon/sight"
    PainSound "blooddemon/pain"
    DeathSound "blooddemon/death"
    ActiveSound "blooddemon/active"
	AttackSound "chainguy/attack"
	Dropitem "Chaingun"
    Obituary "%o was mowed down by a Chaingun Demon."
	Tag "Chaingun Demon"
    States 
    { 
    Spawn: 
       CRG2 AB 10 A_Look
       Loop 
    See: 
       CRG2 A 0 A_PlaySound("blooddemon/walk")
       CRG2 AABB 2 A_Chase
       CRG2 C 0 A_PlaySound("blooddemon/walk")
       CRG2 CCDD 2 A_Chase
       Loop 
    Missile: 
	   CRG2 H 9 A_FaceTarget
       CRG2 EFEFEFEFEF 3 A_CPosAttack
	   CRG2 H 9
	   Goto See
	Pain: 
       CRG2 H 2
       CRG2 H 2 A_Pain
       Goto See
    Death: 
	   TNT1 A 0 A_Pain
	   TNT1 A 0 A_NoBlocking
	   TNT1 A 0 A_SpawnItemEx("CPDChaingunDemonNoGun", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION)
	   Stop 
	Raise:
	   Stop
    } 
}

ACTOR CPDChaingunDemonNoGun
{ 
	//$Category Monsters
	var int user_distance;
    Health 100
    Speed 10
    Height 56
    Radius 30
    Mass 400
    Damage 5
	PainChance 120
	MeleeThreshold 64
	REACTIONTIME 8 
    MONSTER
    +FLOORCLIP
	+FASTMELEE
	+MISSILEMORE
	+MISSILEEVENMORE
    SeeSound "blooddemon/sight"
    PainSound "blooddemon/pain"
    DeathSound "blooddemon/death"
    ActiveSound "blooddemon/active"
    HitObituary "%o Was chewed up and spat out by a Mech-Demon." 
	Tag "Chaingun Demon (No Gun)"
    States 
    { 
    Spawn: 
       SRG2 AB 10 A_Look
       Loop 
    See: 
	   SRG2 A 0 A_PlaySound("blooddemon/walk")
       SRG2 AABB 2 A_Chase
       SRG2 C 0 A_PlaySound("blooddemon/walk")
       SRG2 CCDD 2 A_Chase
	   Loop 
	Missile: 
	   TNT1 A 0 A_SetUserVar("user_distance", 0)
	   TNT1 A 0 A_JumpIfCloser(224, "Bite")
	   TNT1 A 0 A_JumpIfCloser(1024, "Charge")
	   Goto See
	Charge:
	   SRG2 H 1 A_FaceTarget 
	   SRG2 H 1 A_SkullAttack
	   SRG2 A 2 A_PlaySound("blooddemon/walk")
	   SRG2 B 2 A_JumpIfCloser(224, "Bite")
	   SRG2 C 2 A_PlaySound("blooddemon/walk")
	   SRG2 D 2 A_JumpIfCloser(224, "Bite")
	   TNT1 A 0 A_SetUserVar("user_distance", user_distance+1)
	   TNT1 A 0 A_JumpIf(user_distance == 4, "StopCharge")
	   Goto Charge+2
	Bite:
	   SRG2 E 6 A_FaceTarget 
	   SRG2 F 0 A_SkullAttack
	   SRG2 F 0 A_PlaySound("blooddemon/melee")
	   SRG2 FG 6 
	   Goto StopCharge
	StopCharge:
	   TNT1 A 0 A_Gravity
	   TNT1 A 0 A_Stop
	   Goto See
    Melee: 
	   SRG2 E 0 A_PlaySound("blooddemon/melee")
       SRG2 EF 8 A_FaceTarget
       SRG2 G 8 A_CustomMeleeAttack(random(2,9)*5,"none","none")
       Goto See 
    Pain: 
       SRG2 H 2
       SRG2 H 2 A_Pain
       Goto See
    Death: 
       SRG2 I 8 
       SRG2 I 0 A_FaceTarget
       SRG2 J 0 A_SpawnItemEx("BloodDemonArm", 10, 0, 32, 0, 8, 0, 0, 128)
       SRG2 J 8 A_Scream
       SRG2 K 4 
       SRG2 L 4 A_NoBlocking
       SRG2 M 4 
       SRG2 N -1 
       Stop 
    Raise: 
       SRG2 NMLKJI 5 
       Goto See 
    } 
}

ACTOR CPDBlindMechDemon
{
	Health 200
	Radius 30
	Height 56
	Speed 5
	PainChance 100
	Mass 400
	+FLOORCLIP
	Monster
	SeeSound "BPinky/Sight"
	DeathSound "BPinky/Death"
	ActiveSound "Barks"
	PainSound "BPinky/Pain"
	Obituary "%o was too loud and attracted a Blind Mech-Demon."
	Tag "Blind Mech-Demon"
	States
	{
		Spawn:
			BMCD A 4 A_LookEx(0,0,100)
			BMCD AAA 4 A_Wander
			BMCD B 4 A_LookEx(0,0,100)
			BMCD BBB 4 A_Wander
			BMCD C 4 A_LookEx(0,0,100)
			BMCD CCC 4 A_Wander
			BMCD D 4 A_LookEx(0,0,100)
			BMCD DDD 4 A_Wander
			TNT1 A 0 A_Jump(14, "Grumble")
			loop
		See:
			TNT1 A 0 A_SetSpeed(15)
			BMCD AABBCCDD 2 A_Chase
			BMCD AABBCCDD 2 A_Chase
			BMCD AABBCCDD 2 A_Chase
			TNT1 A 0 A_Jump(64, "See", "EndChase")
			loop
		Melee:
			TNT1 A 0 A_PlaySound("BPinky/Bite")
			BMCD EF 6 A_FaceTarget
			BMCD G 6 A_CustomMeleeAttack(random(3, 9) * 5, "", "Melee", true)
			Goto See
		EndChase:
			TNT1 A 0 A_SetSpeed(5)
			BMCD A 1 A_ClearTarget
			Goto Spawn
		Grumble:
			TNT1 A 0 A_PlaySound("BPinky/Idle")
			Goto Spawn
		Pain:
			BMCD H 2 A_Pain
			Goto See
		Death:
			BMCD I 8 
			BMCD J 8 A_Scream
			BMCD K 4 
			BMCD L 4 A_NoBlocking
			BMCD M 4 
			BMCD N -1 
			Stop
	}
}

Actor CPDCyberPinky
{
		Health 250
		PainChance 180
		Speed 14
		Radius 30
		Height 56
		Mass 400
		Monster
		+FLOORCLIP
		SeeSound "demon/sight"
		AttackSound "demon/melee"
		PainSound "demon/pain"
		DeathSound "demon/death"
		ActiveSound "demon/active"
		Obituary "%o was fizzled out by a Cyber Pinky."
		HitObituary "%o was eaten by a Cyber Pinky."
		Tag "Cyber Pinky"
		BloodColor Green
	States
	{
	Spawn:
		CBPK AB 10 A_Look
		Loop
	See:
		CBPK AABBCCDD 2 Fast A_Chase
		Loop
	Melee:
		CBPK EF 6 Fast A_FaceTarget
		CBPK G 6 Fast A_SargAttack
		Goto See
	Missile: 
		CBPK EF 6 Fast A_FaceTarget
		CBPK G 6 Fast A_CustomMissile("RedPlasmaBall", 32)
		Goto See
	Pain:
		CBPK H 2 Fast
		CBPK H 2 Fast A_Pain
		Goto See
	Death:
		CBPK I 8
		CBPK J 8 A_Scream
		CBPK K 4
		CBPK L 4 A_NoBlocking
		CBPK M 4
		CBPK N -1
		Stop
	Raise:
		CBPK N 5
		CBPK MLKJI 5
		Goto See
	}
}

Actor CPDMetalDemon
{
		Health 250
		PainChance 180
		Speed 6
		Radius 30
		Height 56
		Mass 400
		Monster
		+FLOORCLIP
		SeeSound "demon/sight"
		AttackSound "demon/melee"
		PainSound "demon/pain"
		DeathSound "demon/death"
		ActiveSound "demon/active"
		Obituary "%o was chomped into viscera by a Metal Demon."
		Tag "Metal Demon"
		BloodColor White
	States
	{
	Spawn:
		MTDM AB 10 A_Look
		Loop
	See:
		MTDM AABBCCDD 2 Fast A_Chase
		Loop
	Melee:
		MTDM EF 8 Fast A_FaceTarget
		MTDM G 8 Fast A_CustomMeleeAttack(random(1, 10) * 8)
		Goto See
	Pain:
		MTDM H 2 Fast
		MTDM H 2 Fast A_Pain
		Goto See
	Death:
		MTDM I 8
		MTDM J 8 A_Scream
		MTDM K 4
		MTDM L 4 A_NoBlocking
		MTDM M 4
		MTDM N -1
		Stop
	Raise:
		MTDM N 5
		MTDM MLKJI 5
		Goto See
	}
}

actor CPDSuicideBomber
{
  obituary "%o was killed by a crazy Suicide Bomber."
  health 200
  radius 20
  height 56
  mass 100
  speed 10
  painchance 20
  seesound "suicide/sit"
  attacksound "shotguy/death"
  painsound "shotguy/pain"
  deathsound "shotguy/death"
  MONSTER
  +FLOORCLIP
  Tag "Suicide Bomber"
  states
  {
  Spawn:
    BMBE AB 10 A_Look
    loop
  See:
    BMBE AAABBBCCCDDD 2 A_Chase
    BMBE A 0 A_PlaySound("suicide/scream")
    loop
  Melee:
    BMBE A 0 A_Die
    stop
  Pain:
    BMBE A 3
    BMBE A 3 A_Pain
    goto See
  Death:
    BMBE M 0 bright A_Explode (50, 128, XF_NOTMISSILE)
    BMBE M 5 bright A_PlaySound ("weapons/rocklx")
    BMBE N 5 bright A_XScream
    BMBE O 5 A_NoBlocking
    BMBE PQRST 5
    BMBE U -1
    stop
  }
}

ACTOR CPDIronDemon
{
  Health 250
  PainChance 180
  Speed 10
  Radius 30
  Height 56
  Mass 400
  Monster
  +FLOORCLIP
  SeeSound "demon/sight"
  AttackSound "demon/melee"
  PainSound "demon/pain"
  DeathSound "weapons/rocklx"
  ActiveSound "demon/active"
  Obituary "%o was given a lead handshake by an Iron Demon."
  Tag "Iron Demon"
  States
  {
  Spawn:
    IFN2 AB 10 A_Look
    Loop
  See:
    IFIN AABBCCDD 2 A_Chase
    Loop
  Missile:
  Melee:
    IFIN E 10 A_FaceTarget
    IFIN F 10 A_SposAttack
    Goto See
  Pain:
    IFIN G 2
    IFIN G 2 A_Pain
    Goto See
  Death:
	IFIN H 4
    IFIN I 4 A_Scream
    IFIN J 4 A_Explode
    IFIN K 4
    IFIN L 4 A_NoBlocking
    IFIN M 4
    IFIN N -1
    Stop
  }
}

Actor CPDMechtre : CPDMechDemon
{
	+SHADOW
	RenderStyle Translucent
	DropItem BlurSphere 16
    HitObituary "%o didn't see a Mechtre."
	Tag "Mechtre"
	Alpha 0.5
}

Actor CPDBloodLurker : CPDBloodfiend
{
	+SHADOW
	RenderStyle Translucent
	DropItem BlurSphere 16
    HitObituary "%o didn't see a Blood Lurker."
	Obituary "%o didn't see where the blood attacks of a Blood Lurker are coming from."
	Tag "Blood Lurker"
	Alpha 0.5
}

Actor CPDCyberLurker : CPDCyberFiend
{
	+SHADOW
	RenderStyle Translucent
	DropItem BlurSphere 16
    HitObituary "%o didn't see a Cyber Lurker."
	Obituary "%o didn't see where the body parts of a Cyber Lurker are coming from."
	Tag "Cyber Lurker"
	Alpha 0.5
}

Actor CPDChaingunSpectre : CPDChaingunDemon
{
	+SHADOW
	RenderStyle Translucent
    HitObituary "%o didn't see a Chaingun Spectre."
	Obituary "%o didn't see where the bullets of a Chaingun Spectre are coming from."
	Tag "Chaingun Spectre"
	Alpha 0.5
	States {
    Death: 
	   TNT1 A 0 A_Pain
	   TNT1 A 0 A_NoBlocking
	   TNT1 A 0 A_SpawnItemEx("CPDChaingunSpectreNoGun", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION)
	   Stop 
	}
}

Actor CPDChaingunSpectreNoGun : CPDChaingunDemonNoGun
{
	+SHADOW
	RenderStyle Translucent
	DropItem BlurSphere 16
    HitObituary "%o didn't see a Mechtre."
	Tag "Chaingun Spectre (No Gun)"
	Alpha 0.5
}

Actor CPDBlindMechtre : CPDBlindMechDemon
{
	+SHADOW
	RenderStyle Translucent
	DropItem BlurSphere 16
    HitObituary "%o didn't see a Blind Mechtre."
	Tag "Blind Mechtre"
	Alpha 0.5
}

Actor CPDCyberSpectre : CPDCyberPinky
{
	+SHADOW
	RenderStyle Translucent
	DropItem BlurSphere 16
    HitObituary "%o didn't see a Cyber Spectre."
	Obituary "%o didn't see where the plasma balls of a Cyber Spectre are coming from."
	Tag "Cyber Spectre"
	Alpha 0.5
}

Actor CPDMetalSpectre : CPDMetalDemon
{
	+SHADOW
	RenderStyle Translucent
	DropItem BlurSphere 16
    HitObituary "%o didn't see a Metal Spectre."
	Tag "Metal Spectre"
	Alpha 0.5
}

Actor CPDSpectralBomber : CPDSuicideBomber
{
	+SHADOW
	RenderStyle Translucent
	DropItem BlurSphere 16
    HitObituary "%o didn't see a Spectral Bomber."
	Tag "Spectral Bomber"
	Alpha 0.5
}

Actor CPDIronSpectre : CPDIronDemon
{
	+SHADOW
	RenderStyle Translucent
	DropItem BlurSphere 16
    Obituary "%o didn't see where the shotgun pellets of an Iron Spectre are coming from."
	Tag "Iron Spectre"
	Alpha 0.5
}