Actor CPDCacoBot {
  Health 450
  Radius 31
  Height 56
  Mass 400
  Speed 8
  PainChance 128
  Monster
  +FLOAT
  +NOGRAVITY
  SeeSound "cacobot/sight"
  PainSound "cacobot/pain"
  DeathSound "cacobot/death"
  ActiveSound "cacobot/active"
  Obituary "%o was vaporized by a Cacobot."
  Tag "Cacobot"
  States
  {
  Spawn:
    CABT A 10 A_Look
    Loop
  See:
    CABT A 3 A_Chase
    Loop
  Missile:
	CABT A 0 A_Jump(128, "MissilePlasma")
    CABT BC 5 A_FaceTarget
    CABT D 5 Bright A_CustomComboAttack("CacodemonBall", 24, 10 * random(1, 6))
    Goto See
  MissilePlasma:
    CABT M 10 A_FaceTarget
    CABT N 5 Bright A_CustomMissile("RedPlasmaBall", 32)
    Goto See
  Pain:
    CABT E 3
    CABT E 3 A_Pain
    CABT F 6
    Goto See
  Death:
    CABT G 8
    CABT H 8 A_Scream
    CABT IJ 8
    CABT K 8 A_NoBlocking
    CABT L -1 A_SetFloorClip
    Stop
  Raise:
    CABT L 8 A_UnSetFloorClip
    CABT KJIHG 8
    Goto See
  }
}

Actor CPDCyberCaco {
  Health 400
  Radius 31
  Height 56
  Mass 400
  Speed 10
  PainChance 128
  Monster
  +FLOAT
  +NOGRAVITY
  SeeSound "cybercaco/sight"
  PainSound "cybercaco/pain"
  DeathSound "cybercaco/death"
  ActiveSound "caco/active"
  Obituary "%o was vaporized by a Cyber Cacodemon."
  Tag "Cyber Cacodemon"
  States
  {
  Spawn:
    CCAC A 10 A_Look
    Loop
  See:
    CCAC A 3 A_Chase
    Loop
  Missile:
    CCAC BC 5 A_FaceTarget
    CCAC D 5 Bright A_HeadAttack
	CCAC D 0 A_Jump(200, "Missile")
    Goto See
  Pain:
    CCAC E 6 A_Pain
    CCAC E 6
    Goto See
  Death:
    CCAC F 8 A_Scream
    CCAC GH 8
    CCAC I 8 A_NoBlocking
    CCAC J -1 A_SetFloorClip
    Stop
  Raise:
    CCAC J 8 A_UnSetFloorClip
    CCAC IHGF 8
    Goto See
  }
}

ACTOR CPDCacolantern
{ 
	//$Category "ZDoom Monster pack"
   Health 500 
   Speed 8 
   Radius 31
   Height 56
   PainChance 128
   Mass 400
   Meleedamage 6 
   REACTIONTIME 8 
   BloodColor "EB 6F 0F"
   MONSTER
   +NOGRAVITY
   +FLOAT
   SeeSound "caco/sight"
   PainSound "caco/pain"
   DeathSound "caco/death"
   ActiveSound "caco/active"
   Missiletype CacolanternBall 
   MissileHeight 30
   HitObituary "%o admired the Cacolantern's teeth." 
   Obituary "%o couldn't take %h eyes off the Cacolantern's eye."
   Tag "Cacolantern"
   States 
   { 
   Spawn: 
      HED2 A 10 bright A_Look 
      Loop 
   See: 
      HED2 A 3 bright A_Chase 
      Loop 
   Missile: 
      HED2 BC 5 bright A_FaceTarget 
      HED2 D 5 bright A_ComboAttack
      Goto See 
   Pain: 
      HED2 E 4 bright
      HED2 E 4 bright A_Pain 
      HED2 F 5 bright
      Goto See 
   Death: 
      HED2 G 8 bright
      HED2 H 8 bright A_Scream 
      HED2 I 8 bright
      HED2 J 8
      HED2 K 8 A_NoBlocking 
      HED2 L -1 A_SetFloorClip
      Stop 
   Raise: 
      HED2 L 8 A_UnsetFloorClip
      HED2 KJ 8
      HED2 IHG 8 bright 
      Goto See 
   } 
}

actor CacolanternBall
{
    Radius 13
    Height 8
    Speed 20
    Damage 9
    PROJECTILE
    RENDERSTYLE ADD
    ALPHA 0.67
    SeeSound "imp/attack"
    DeathSound "imp/shotx"
    Decal CacoScorch
    States
    {
    Spawn:
        BAL8 ABCDEFGH 5 Bright
        Loop
    Death:
        BAL8 IJKL 4 Bright
        Stop
    }
}

Actor CPDAbaddon
{
Monster
Health 750
Radius 36
Height 60
Mass 2000
Speed 12
PainChance 40
Scale 1.1
MeleeRange 56
SeeSound "Abaddon/Sight"
ActiveSound "Abaddon/Active"
PainSound "Abaddon/Pain"
DeathSound "Abaddon/Death"
Obituary "%o was smitten by an Abaddon."
HitObituary "%o became food for an Abaddon."
Tag "Abaddon"
+FLOAT
+NOGRAVITY
States
	{
	Spawn:
		HED3 A 10 A_Look
		Loop
	See:
		HED3 A 3 A_Chase
		Loop
	Pain:
		HED3 E 2
		HED3 E 2 A_Pain
		HED3 F 4
		Goto See
	Missile:
		TNT1 A 0 A_Jump(130,7,13)
		HED3 BC 5 A_FaceTarget
		HED3 D 5 Bright A_BasicAttack(8,"none","AbaddonBall",28)
		HED3 BC 5 A_FaceTarget
		HED3 D 5 Bright A_BasicAttack(8,"none","AbaddonBall",28)
		Goto See
		HED3 BC 5 A_FaceTarget
		HED3 D 1 Bright A_CustomMissile("AbaddonBallSmall",28,3,8)
		HED3 D 1 Bright A_CustomMissile("AbaddonBallSmall",28,0,0)
		HED3 D 3 Bright A_CustomMissile("AbaddonBallSmall",28,-3,-8)
		TNT1 A 0 A_Jump(80,7)
		Goto See
		HED3 BC 5 A_FaceTarget
		HED3 D 1 Bright A_CustomMissile("AbaddonBallSmall",28,-3,-8)
		HED3 D 1 Bright A_CustomMissile("AbaddonBallSmall",28,0,0)
		HED3 D 3 Bright A_CustomMissile("AbaddonBallSmall",28,3,8)
		TNT1 A 0 A_Jump(80,1)
		Goto See
		HED3 BC 5 A_FaceTarget
		HED3 D 5 Bright A_BasicAttack(8,"none","AbaddonBall",28)
		Goto See
	Death:
		HED3 G 6 A_NoBlocking
		HED3 H 6 A_Scream
		HED3 IJKLMN 6
		HED3 O -1 A_SetFloorClip
		Stop
	Raise:
		HED3 O 6 A_UnSetFloorClip
		HED3 NMLKJIHG 6
		Goto See
	}
}

Actor AbaddonBall
{
Projectile
Radius 6
Height 8
Speed 20
FastSpeed 30
Damage 10
RenderStyle Add
Decal Cacoscorch
SeeSound "caco/attack"
DeathSound "caco/shotx"
+RANDOMIZE
States
	{
	Spawn:
		BAL3 AB 4 BRIGHT
		Loop
	Death:
		BAL3 CDE 6 BRIGHT
		Stop
	}
}

Actor AbaddonBallSmall : CacodemonBall
{
Radius 4
Height 6
Speed 16
FastSpeed 24
Decal HImpScorch
States
	{
	Spawn:
		BAL3 FGH 4 Bright
		Loop
	Death:
		BAL3 IJKLM 4 Bright
		Stop
	}
}

ACTOR CPDEnhancedCacodemon
{ 
	//$Category "ZDoom Monster pack"
   Health 500
   Speed 8 
   Radius 31
   Height 56
   PainChance 128
   Mass 400
   Meleedamage 6 
   REACTIONTIME 8 
   MONSTER
   +NOGRAVITY
   +FLOAT
   SeeSound "caco/sight"
   PainSound "caco/pain"
   DeathSound "caco/death"
   ActiveSound "caco/active"
   HitObituary "%o was mowed down by an Enhanced Cacodemon." 
   Obituary "%o was made into a snack by the Enhanced Cacodemon."
   Tag "Enhanced Cacodemon"
   States 
   { 
   Spawn: 
      ENCD A 10 A_Look 
      Loop 
   See: 
      ENCD A 3 A_Chase 
      Loop 
   Missile: 
      ENCD BC 5 A_FaceTarget 
      ENCD B 0 bright A_CustomMissile("CacodemonBall", 24, 0, 0, 1)
      ENCD B 0 bright A_CustomMissile("CacodemonBall", 24, 0, -8, 1)
      ENCD B 5 bright A_CustomMissile("CacodemonBall", 24, 0, 8, 1)
      Goto See 
   Pain: 
      ENCD D 4 
      ENCD D 4 A_Pain 
      ENCD D 5 
      Goto See 
   Death: 
      ENCD E 8 
      ENCD F 8 A_Scream 
      ENCD GH 8 
      ENCD I 8 A_NoBlocking 
      ENCD J -1 A_SetFloorClip
      Stop 
   Raise: 
      ENCD J 8 A_UnsetFloorClip
      ENCD IHGFE 8 
      Goto See 
   } 
}

